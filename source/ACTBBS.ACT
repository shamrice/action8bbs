; Action!8! BBS
;
; Start Date: 2025-07-11
; Last update: 2026-02-13

BYTE RTS = [$60]

;INCLUDE "D1:SYSBBS.ACT"

MODULE

DEFINE FIOCB="6"
BYTE IMASK=[0],ALLOW_INVERTED=[0]
BYTE GFXMODE=[0],UCLOG=[0]
BYTE c=[0],i=[0],v=[0]

BYTE ARRAY lOffset(25)
CARD ARRAY lSect(25)


INCLUDE "D1:NETWORK.ACT"
INCLUDE "D1:TIME.ACT"
INCLUDE "D1:FILE.ACT"
INCLUDE "D1:UTIL.ACT"
INCLUDE "D1:MSGBASE1.ACT"
INCLUDE "D1:MSGBASE2.ACT"
INCLUDE "D1:MSGBASE3.ACT"
INCLUDE "D1:ACCT1.ACT"
INCLUDE "D1:ACCTATTR.ACT"
INCLUDE "D1:WALL.ACT"
INCLUDE "D1:NEBBSAPI.ACT"
INCLUDE "D1:SURVEY.ACT"

PROC AcceptConn()
  BYTE KB = $02FC

  Close(2)
  Open(2,"K:",4,0)

  PrintE("Waiting. Press # to shutdown")

  WHILE IS_CONN = 0
  DO
    TSleep(10)

    NCmd('A)
    NGetStatus()
    IF ERRNO = 1 THEN
      PutE()
      PrintE("CONNECT")
      IS_CONN = 1
      GFXMODE = 0
      IGNORE_CONN_CLOSED = 0
      ALLOW_INVERTED = 0
      ; reset session timer..
      TSEXP = TS + 1

      NStatus()
    ELSE
      IF KB <> $FF THEN
        c = GetD(2)
        IF c = '# THEN
          PrintE("WARN! Server shutdown!")
          IS_CONN = 0
          IMASK = '#
          EXIT
        FI
      FI
    FI
  OD

  Close(2)
RETURN


PROC HandleUserConn()
  c = 0
  v = 0

  NWriteSE("Press RETURN to continue....")
  DO
    c = NGetB()
    IF c < 155 THEN
      v==+1
    FI
  UNTIL IS_CONN = 0 OR c = 155 OR v = 10
  OD
  v = 0
  IF c <> 155 THEN
    IS_CONN = 0
    RETURN
  FI

  TSEXP = TS + 1

  IF DoLogin() = 0 THEN
    IS_CONN = 0
    RETURN
  FI

  UCLOG = 1
  TSEXP = TS + 2

  SetUAttr()

  ;increment login count
  UATTR(4)==+1
  IF UATTR(4) = 0 THEN
    UATTR(3)==+1
  FI

  NPrintFile("D1:TITLE.ATA")
  NGetB()

  NPrintFile("D1:MAINMENU.ATA")

  c = 0

  WHILE IS_CONN = 1
  DO
    NWriteS("MAIN> ")
    c = NGetB()

    IF c >= 97 THEN
      c==-32
    FI

    IF c = 'G THEN
      NPrintFile("D1:LOGOUT.ATA")
      IS_CONN = 0

    ELSEIF c = 'C THEN
      NPrintFile("D2:CALL.LOG")

    ELSEIF c = 'P THEN
      PostMsg(255) ;255 = no parent

    ELSEIF c = 'R THEN
      ReadMsgs()

    ELSEIF c = 'B THEN
      NPrintFile("D2:SYSINFO.TXT")

    ELSEIF c = '/ THEN
      UAttrConfig()

    ELSEIF c = 'U THEN
      NPrintFile(ULNAME)

    ELSEIF c = 'W THEN
      Wall()

    ELSEIF c = 'S THEN
      SurveyMenu()

    ELSEIF c = 'I THEN
      NPutC(155)
      NWriteSE("Sorry, no easter egg this time!")
      NWriteSE("...ran out of disk space!")
      NPutC(155)
;     NPrintFile("D1:INFIL.ATA")

    ELSEIF c = '? THEN
      NPrintFile("D1:MAINMENU.ATA")

    ELSEIF c = 'N THEN
      NELatest()

    ELSEIF c = '# THEN
      IF UATTR(9) = 255 THEN
        IS_CONN = 0
        IMASK = '#
        PrintE("WARN>> Server shutdown by user!")
      FI
    ELSE
      NWriteSE("Press ? for help")
    FI

  OD
RETURN


PROC Main()
  Put(125)
  PrintE("           ACTION8BBS             ")
  Close(5)
  Close(FIOCB)

  NOpen()
  NGetStatus()
  IF ERRNO <> 1 THEN
    NCmd('C)
    PrintE("ERROR opening connection!")
    PrintF("ERROR: %B%E", ERRNO)
    RETURN
  FI

  SetHIMSG()
  IMASK=0
  IS_CONN = 0

  DO
    UCLOG = 0
    AcceptConn()

    IF IS_CONN = 1 THEN
      HandleUserConn()
      TSleep(30)
      NCmd('c)
    FI

    IF IMASK = '# THEN
      EXIT
    FI

    IF UCLOG = 1 THEN
      Print("LOGOFF DATE: ")
      NGetDT()
      Print("LOGOFF USER: ")
      FOR i = 0 TO 9
      DO
        RXBUFF(i+18) = UNAME(i)
        Put(UNAME(i))
      OD
      PutE()
      AppendFile("D2:CALL.LOG",RXBUFF,27)

      SaveUAttr()
    FI
    PutE()
  OD

  Close(FIOCB)

  NCmd('C)

  PrintE("Server offline.")

RETURN

