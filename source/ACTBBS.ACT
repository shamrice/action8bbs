; Action!8! BBS
;
; Start Date: 2025-07-11
; Last update: 2025-09-19

BYTE RTS = [$60]

;INCLUDE "D3:SYS.ACT"

MODULE

DEFINE FIOCB="6"
BYTE IMASK=[0]
BYTE GFXMODE=[0],UCLOG=[0]
BYTE c=[0],i=[0],v=[0]

INCLUDE "D1:NETWORK.ACT"
INCLUDE "D1:TIME.ACT"
INCLUDE "D1:FILE.ACT"
INCLUDE "D1:UTIL.ACT"
INCLUDE "D1:ACCT1.ACT"
INCLUDE "D1:MSGBASE1.ACT"
INCLUDE "D1:WALL.ACT"

PROC AcceptConn()
  Print("Waiting")

  WHILE IS_CONN = 0
  DO
    TSleep(10)

    NCmd('A)
    NGetStatus()
    IF ERRNO = 1 THEN
      PutE()
      PrintE("CONNECT")
      IS_CONN = 1
      GFXMODE = 0
      ; reset session timer..
      TSEXP = TS + 1

      NStatus()
    FI
  OD
RETURN


PROC HandleUserConn()
  BYTE ARRAY temp(40)
  CARD uAttrTmp=[0]
  c = 0
  v = 0
  uAttrTmp = 0

  NWriteSE("Press RETURN to continue....")
  DO
    c = NGetB()
    IF c < 155 THEN
      v==+1
    FI
  UNTIL IS_CONN = 0 OR c = 155 OR v = 10
  OD
  v = 0
  IF c <> 155 THEN
    IS_CONN = 0
    RETURN
  FI

  TSEXP = TS + 1

  IF DoLogin() = 0 THEN
    IS_CONN = 0
    RETURN
  FI

  UCLOG = 1
  TSEXP = TS + 2

  SetUAttr()

  ;increment login count
  UATTR(4)==+1
  IF UATTR(4) = 0 THEN
    UATTR(3)==+1
  FI

  NPutC(125)
  NPrintFile("D2:TITLE.ATA")
  NGetB()

  NPutC(125)
  NPrintFile("D2:MAINMENU.ATA")

  c = 0

  WHILE IS_CONN = 1
  DO
    NWriteS("MAIN> ")
    c = NGetB()

    IF c >= 97 THEN
      c==-32
    FI

    IF c = 'G THEN
      NPrintFile("D2:LOGOUT.ATA")
      IS_CONN = 0

    ELSEIF c = 'C THEN
      NPrintFile("D2:CALL.LOG")

    ELSEIF c = 'P THEN
      PostMsg()

    ELSEIF c = 'R THEN
      ReadMsgs()

    ELSEIF c = 'S THEN
      NPrintFile("D2:SYSINFO.TXT")

    ELSEIF c = '/ THEN
      ; temp debug print user attrs
      ; TODO: make its own PROC
      NPutC(125)
      NWriteSE("          ACCOUNT STATS:          ")
      NWriteSE("")
      NPutC(155)
      NWriteS("ACCOUNT CREATED: ")
      StrB(UATTR(0),temp)
      NWriteS(temp)
      NWriteS("/")
      StrB(UATTR(1),temp)
      NWriteS(temp)
      NWriteS("/")
      StrB(UATTR(2),temp)
      NWriteSE(temp)
      NWriteS("     NUM LOGINS: ")
      uAttrTmp = UATTR(4)
      IF UATTR(3) > 0 THEN
        uAttrTmp==*UATTR(3)
      FI
      StrC(uAttrTmp,temp)
      NWriteSE(temp)
      NWriteS("    LAST HI-MSG: ")
      StrB(UATTR(5),temp)
      NWriteSE(temp)

      NWriteS(" NUM POSTS MADE: ")
      uAttrTmp = UATTR(7)
      IF UATTR(6) > 0 THEN
        uAttrTmp==*UATTR(6)
      FI
      StrC(uAttrTmp,temp)
      NWriteSE(temp)
      NPutC(155)

    ELSEIF c = 'U THEN
      NPrintFile(ULNAME)

    ELSEIF c = 'W THEN
      Wall()

    ELSEIF c = '? THEN
      NPrintFile("D2:MAINMENU.ATA")

    ELSEIF c = '# THEN
      IS_CONN = 0
      IMASK = '#
    ELSE
      NWriteSE("Press ? for help")
    FI

  OD
RETURN


PROC Main()
  Put(125)
  PrintE("           ACTION8BBS             ")
  Close(FIOCB)

  NOpen()
  NGetStatus()
  IF ERRNO <> 1 THEN
    NCmd('C)
    RETURN
  FI

  SetHIMSG()

  DO
    UCLOG = 0
    AcceptConn()
    HandleUserConn()
    NCmd('c)

    IF IMASK = '# THEN
      EXIT
    FI

    IF UCLOG = 1 THEN
      NGetDT()
      FOR i = 0 TO 9
      DO
        RXBUFF(i+18) = UNAME(i)
      OD
      AppendFile("D2:CALL.LOG",RXBUFF,27)

      SaveUAttr()
    FI

  OD

  Close(FIOCB)

  NCmd('C)

RETURN

