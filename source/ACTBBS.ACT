; Action! BBS
;
; Start Date: 2025-07-11
; Last update: 2025-09-05

BYTE RTS = [$60]

;INCLUDE "D3:SYS.ACT"

MODULE

DEFINE FIOCB="6"
BYTE IMASK=[0]
BYTE GFXMODE=[0],UCLOG=[0]
BYTE c=[0],i=[0],v=[0]

INCLUDE "D3:NETWORK.ACT"
INCLUDE "D3:TIME.ACT"
INCLUDE "D3:FILE.ACT"
INCLUDE "D3:UTIL.ACT"

CARD FUNC ReadInp()
  CARD readLen=[0]

; IF TRIP = 0 THEN
;   RETURN (0)
; FI

  NStatus()

  IF BYTESWAITING = 0 THEN
    RETURN (0)
  FI

  IF BYTESWAITING > 38 THEN
    BYTESWAITING = 38
  FI
  readLen = BYTESWAITING
  NRead(NSB,readLen)

  TRIP = 0
  PACTL = PACTL % 1

RETURN (readLen)

PROC GetInp()
  CARD readLen=[0]

  readLen = 0
  c = 0

  FOR i = 0 TO 39
  DO
    RXBUFF(i) = 32
    NSB(i) = 32
  OD

  IF IS_CONN = 0 THEN
    RETURN
  FI

  WHILE IS_CONN = 1
  DO
    readlen = ReadInp()

    IF readLen > 0 THEN

      readLen==-1
      FOR i = 0 TO readLen
      DO

        IF NSB(i) = 155 THEN
          NPutC(155)
          RETURN
        ELSEIF NSB(i) = 126 AND c > 0 THEN
          NPutC(126)
          RXBUFF(c) = 32
          c==-1
        ELSEIF c < 38 THEN

          IF GFXMODE = 0 AND (NSB(i) < 32 OR NSB(i) > 122) THEN
            ; IGNORE
          ELSE
            RXBUFF(c) = NSB(i)
            IF IMASK > 0 THEN
              NSB(i) = IMASK
            FI
            NPutC(NSB(i))
            c==+1
          FI
        FI
      OD
    ELSE
      IF TSessExp() = 1 THEN
        RETURN
      FI
    FI
  OD

RETURN

INCLUDE "D3:ACCT1.ACT"
INCLUDE "D3:MSGBASE1.ACT"
INCLUDE "D3:WALL.ACT"

PROC AcceptConn()
  Print("Waiting")

  WHILE IS_CONN = 0
  DO
    TSleep(10)

    NCmd('A)
    NGetStatus()
    IF ERRNO = 1 THEN
      PutE()
      PrintE("CONNECT")
      IS_CONN = 1
      GFXMODE = 0
      ; reset session timer..
      TSEXP = TS + 1

      NStatus()
    FI
  OD
RETURN


PROC HandleUserConn()
  c = 0
  v = 0

  NWriteSE("Press RETURN to continue....")
  DO
    c = NGetB()
    IF c < 155 THEN
      v==+1
    FI
  UNTIL IS_CONN = 0 OR c = 155 OR v = 10
  OD
  v = 0
  IF c <> 155 THEN
    IS_CONN = 0
    RETURN
  FI

  TSEXP = TS + 1

  IF DoLogin() = 0 THEN
    IS_CONN = 0
    RETURN
  FI

  TSEXP = TS + 2

  UCLOG = 1
  NPutC(125)
  NPrintFile("D2:TITLE.ATA")
  NGetB()

  NPutC(125)
  NPrintFile("D2:MAINMENU.ATA")

  c = 0

  WHILE IS_CONN = 1
  DO
    NWriteS("MAIN> ")
    c = NGetB()

    IF c >= 97 THEN
      c==-32
    FI

    IF c = 'G THEN
      NPrintFile("D2:LOGOUT.ATA")
      IS_CONN = 0

    ELSEIF c = 'C THEN
      NPrintFile("D2:CALL.LOG")

    ELSEIF c = 'P THEN
      PostMsg()

    ELSEIF c = 'R THEN
      ReadMsgs()

    ELSEIF c = 'S THEN
      NPrintFile("D2:SYSINFO.TXT")

    ELSEIF c = 'U THEN
      NPrintFile(ULNAME)

    ELSEIF c = 'W THEN
      Wall()

    ELSEIF c = '? THEN
      NPrintFile("D2:MAINMENU.ATA")

    ELSEIF c = '# THEN
      IS_CONN = 0
      IMASK = '#
    ELSE
      NWriteSE("Press ? for help")
    FI

  OD
RETURN


PROC Main()
  Close(FIOCB)

  NOpen()
  NGetStatus()
  IF ERRNO <> 1 THEN
    NCmd('C)
    RETURN
  FI

  SetHIMSG()

  DO
    UCLOG = 0
    AcceptConn()
    HandleUserConn()
    NCmd('c)

    IF IMASK = '# THEN
      EXIT
    FI

    IF UCLOG = 1 THEN
      NGetDT()
      FOR i = 0 TO 9
      DO
        RXBUFF(i+18) = UNAME(i)
      OD
      AppendFile("D2:CALL.LOG",RXBUFF,27)
    FI

  OD

  Close(FIOCB)

  NCmd('C)

RETURN

