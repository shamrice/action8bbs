MODULE

BYTE MO=[0]
BYTE HIMSGID=[0]
BYTE MBROLL=[0]
BYTE LASTPID=[0]

CARD MS=[0]

BYTE ARRAY MBFNAME(0)="D2:MSGBASE.DAT"
BYTE ARRAY MIFNAME(0)="D2:MSGBASE.IDX"
BYTE ARRAY MHFNAME(0)="D2:MSGHI.DAT"
BYTE ARRAY MTEMPNAME(0)="D2:MSGTEMP.DAT"
BYTE ARRAY PREVSUBJ(30)
BYTE ARRAY PREVFROM(30)

PROC SetHIMSG()
  Open(6,MHFNAME,4,0)
  HIMSGID = InputBD(6)
  MBROLL = InputBD(6)
  Close(6)
RETURN


PROC SetMsgCoords(BYTE id)

  BYTE l=[0]
  l=0
  MS=0
  MO=0

  Close(6)
  Open(6,MIFNAME,4,0)

  WHILE EOF(6) = 0
  DO
    MS = InputCD(6)
    MO = InputBD(6)

    IF l = id THEN
      EXIT
    FI
    l==+1
  OD

  Close(6)
;  PrintF("ID=%B MS=%U MO=%B%E",id,MS,MO)
RETURN


BYTE FUNC GetRealId(BYTE dId)
  BYTE id=[0]
  id = 0

  PrintF("GetRealId : dId=%B%E",dId)

  IF MBROLL = 1 THEN
    id = dId + HIMSGID
    IF HIMSGID > id THEN
      id==-256
    FI
    PrintF("MBROLLED : realid=%B",id)
  ELSE
    IF dId > HIMSGID THEN
      id = HIMSGID
      PrintF("MB NOT ROLL : id=himsg : id=%B",id)
    ELSEIF dId = 0 THEN
      id = 1
      PrintE("MB NOT ROLL : dId=0 so id=1")
    ELSE
      PrintE("MB NOT ROLL: ELSE, id = dId")
      id = dId
    FI
  FI
  IF id >= 50 THEN
    id==-50
    PrintE("Over 50, id==-50")
  FI
  PrintF("FINAL ID:%B%E",id)
RETURN (id)



BYTE FUNC GetDispId(BYTE id)
  IF MBROLL = 1 THEN
    IF HIMSGID > id THEN
      id = 50 - (HIMSGID - id)
    ELSE
      id==-HIMSGID
    FI
  FI
  IF id = 0 THEN
    id = 50
  FI
RETURN (id)


PROC ViewMsg(BYTE id)

  BYTE c=[0],l=[0],pId=[0],pSubjI=[0]
  BYTE isMore=[0]

  isMore=0

  IF id >= 50 THEN
    PrintF("Id too high: %B%E",id)
    id = HIMSGID
  FI


  ; keep track of user himsgid
  IF id <= HIMSGID THEN
    IF id > UATTR(5) THEN
      PrintF("IF New UHIMSG=%B%E",id)
      UATTR(5) = id
    ELSEIF id < UATTR(5) AND UATTR(5) > HIMSGID THEN
      PrintF("ELSEIF New UHIMSG=%B%E",id)
      UATTR(5) = id
    FI
  FI

  SetMsgCoords(id)

  c=0
  i=0
  l=1
  pSubjI=0
  pId=id

  FOR i = 0 TO 29
  DO
    PREVSUBJ(i)=32
    PREVFROM(i)=32
  OD

  i=0

  Close(5)
  Open(5,MBFNAME,12,0)
  Point(5,MS,MO)

  ;default to no parentid if OOB
  pId = GetD(5)
  IF pId > 49 THEN
    pId = id
  FI

  IF pId > id AND pId <= HIMSGID THEN
    PrintF("pId %B has been cycled out%E",pId)
    pId = id
  FI

  PrintF("View ID:%B PID:%B HI:%B%E",id,pId,HIMSGID)

  LASTPID=pId

  NPutC(125)
  id = GetDispId(id)
  pId = GetDispId(pId)

  NWriteS("  ID: ")
  StrB(id,RXBUFF)
  NWriteS(RXBUFF)

  IF MBROLL = 1 THEN
    NWriteS("/50+")
  ELSE
    NPutC('/)
    StrB(HIMSGID,RXBUFF)
    NWriteS(RXBUFF)
  FI

  ; display parent id if reply
  IF pId <> id THEN
    NWriteS(" [Reply to: #")
    StrB(pId,RXBUFF)
    NWriteS(RXBUFF)
    NWriteSE("]")
  ELSE
    NPutC(155)
  FI

  WHILE EOF(5) = 0
  DO
    RXBUFF(i) = GetD(5)

    IF RXBUFF(i) = 158 THEN
      EXIT
    ELSEIF RXBUFF(i) = 155 THEN
      l==+1
    FI

    ; keep track of subj for replies
    ; there's def a bug here with the
    ; pSubjI var....
    IF isMore=0 AND (l=2 OR l=4) THEN
      pSubjI==+1

      IF pSubjI > 7 AND pSubjI < 38 THEN
        IF l = 2 THEN
          PREVFROM(pSubjI - 8) = RXBUFF(i)
        ELSE
          PREVSUBJ(pSubjI - 8) = RXBUFF(i)
        FI
      FI
    ELSE
      pSubjI = 0
    FI

    i==+1

    IF i > 38 OR l > 18 THEN
      NWrite(RXBUFF,i)
      i = 0
      IF l > 18 THEN
        isMore = 1

        NWriteS("[MORE]")
        NGetB()
        NSB(0) = 126
        NSB(1) = 126
        NSB(2) = 126
        NSB(3) = 126
        NSB(4) = 126
        NSB(5) = 126
        NWrite(NSB,6)
        ;NPutC(155)
        l = 0
      ELSE
        NStatus()

        IF BYTESWAITING > 0 THEN
          NRead(NSB, 1)
;          TRIP = 0
;          PACTL = PACTL % 1
          IF NSB(0) = 3 THEN
            NPutC(155)
           ;PrintE("^C FOUND")
            EXIT
          FI
        FI
      FI
    FI
  OD
  NWrite(RXBUFF,i)


  Put(')
  FOR i = 0 TO 29
  DO
    Put(PREVSUBJ(i))
  OD
  Put(')
  PutE()
  Close(5)

  IF UATTR(8) = 0 THEN
    NWriteSE("_______________________________________")
    NWriteSE(" Next Jump Post Reply Exit ?Help")
  FI

  NPutC(155)

RETURN

