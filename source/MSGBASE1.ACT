MODULE

BYTE MO=[0]
BYTE HIMSGID=[0]
BYTE MBROLL=[0]

CARD MS=[0]

BYTE ARRAY MBFNAME(0)="D2:MSGBASE.DAT"
BYTE ARRAY MIFNAME(0)="D2:MSGBASE.IDX"
BYTE ARRAY MHFNAME(0)="D2:MSGHI.DAT"
BYTE ARRAY MTEMPNAME(0)="D2:MSGTEMP.DAT"

PROC SetHIMSG()
  Open(6,MHFNAME,4,0)
  HIMSGID = InputBD(6)
  MBROLL = InputBD(6)
  Close(6)
RETURN


PROC SetMsgCoords(BYTE id)

  BYTE l=[0]
  l=0
  MS=0
  MO=0

  Close(6)
  Open(6,MIFNAME,4,0)

  WHILE EOF(6) = 0
  DO
    MS = InputCD(6)
    MO = InputBD(6)

    IF l = id THEN
      EXIT
    FI
    l==+1
  OD

  Close(6)
;  PrintF("ID=%B MS=%U MO=%B%E",id,MS,MO)
RETURN


BYTE FUNC GetRealId(BYTE dId)
  BYTE id=[0]
  id = 0

  IF MBROLL = 1 THEN
    id = dId + HIMSGID
    IF HIMSGID > id THEN
      id==-256
    FI
  ELSE
    IF dId > HIMSGID THEN
      id = HIMSGID
    ELSEIF dId = 0 THEN
      id = 1
    FI
  FI
  IF id >= 50 THEN
    id==-50
  FI
RETURN (id)



BYTE FUNC GetDispId(BYTE id)
  IF MBROLL = 1 THEN
    IF HIMSGID > id THEN
      id = 50 - (HIMSGID - id)
    ELSE
      id==-HIMSGID
    FI
  FI
  IF id = 0 THEN
    id = 50
  FI
RETURN (id)


PROC ViewMsg(BYTE id)

  BYTE c=[0],l=[0]

  IF id >= 50 THEN
    PrintF("Id too high: %B%E",id)
    id = HIMSGID
  FI

  ; keep track of user himsgid
  IF id > UATTR(5) AND id <= HIMSGID THEN
    PrintF("New UHIMSG=%B%E",id)
    UATTR(5) = id
  ELSEIF id = 0 THEN
    c = GetDispId(id)
    IF c = 50 THEN
      PrintE("50 found.New UHIMSG")
      UATTR(5) = id
    FI
  FI

  SetMsgCoords(id)

  PrintF("View ID: %B%E",id)
  c=0
  i=0
  l=1

  Close(5)
  Open(5,MBFNAME,12,0)
  Point(5,MS,MO)

  NPutC(125)
  id = GetDispId(id)

  NWriteS("  ID: ")
  StrB(id,RXBUFF)
  NWriteS(RXBUFF)

  IF MBROLL = 1 THEN
    NWriteSE("/50+")
  ELSE
    NPutC('/)
    StrB(HIMSGID,RXBUFF)
    NWriteSE(RXBUFF)
  FI

  WHILE EOF(5) = 0
  DO
    RXBUFF(i) = GetD(5)
    IF RXBUFF(i) = 158 THEN
      EXIT
    ELSEIF RXBUFF(i) = 155 THEN
      l==+1
    FI

    i==+1

    IF i > 38 OR l > 18 THEN
      NWrite(RXBUFF,i)
      i = 0
      IF l > 18 THEN
        NWriteS("[MORE]")
        NGetB()
        NSB(0) = 126
        NSB(1) = 126
        NSB(2) = 126
        NSB(3) = 126
        NSB(4) = 126
        NSB(5) = 126
        NWrite(NSB,6)
        ;NPutC(155)
        l = 0
      ELSE
        NStatus()

        IF BYTESWAITING > 0 THEN
          NRead(NSB, 1)
;          TRIP = 0
;          PACTL = PACTL % 1
          IF NSB(0) = 3 THEN
            NPutC(155)
            PrintE("^C FOUND")
            EXIT
          FI
        FI
      FI
    FI
  OD
  NWrite(RXBUFF,i)

  Close(5)

  IF UATTR(8) = 0 THEN
    NWriteSE("_______________________________________")
    NWriteSE(" Next Msg  Jump  Reply  Exit  ?Help")
  FI

  NPutC(155)

RETURN

