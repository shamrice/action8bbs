MODULE

; Action! BBS Set up program
; Creates the necessary files
; and inits the SysOp user.

PROC CopyFile(BYTE ARRAY src, BYTE ARRAY dest)

  BYTE c=[0]
  c = 0

  PrintF("Copying %S to %S%E",src,dest)

  Close(5)
  Close(6)
  Open(5,src,4,0)
  Open(6,dest,8,0)

  WHILE EOF(5) = 0
  DO
    c = GetD(5)
    PutD(6,c)
  OD
  Close(5)
  Close(6)

RETURN

PROC InitUsers()

  BYTE ARRAY u(10)
  BYTE c=[0],o=[0],i=[0]
  CARD s=[0]
  c=0
  i=0
  o=0
  s=0

  Print("Init User data [0]? ")
  c = InputB()


  IF c = 0 THEN
    RETURN
  FI

  Close(6)
  PrintE("Creating new USERS.LST")
  Open(6, "D2:USERS.LST",8,0)
  PrintDE(6,"")
  PrintDE(6,"  L I S T  O F  U S E R S ")
  PrintDE(6,"")

  Close(6)

  PrintE("Creating new USERS.ATR")
  Open(6, "D2:USERS.ATR",8,0)
  Note(6,@s,@o)
  FOR i = 0 TO 31
  DO
    IF i = 9 THEN
      PutD(6,255) ;SysOp permissions
    ELSE
      PutD(6,0)
    FI
  OD
;  PutD(6,155)
  Close(6)
  PrintF("SysOp ATTR: s=%U o=%B%E",s,o)



  PrintE("Creating new USERS.DAT")

  PrintE("Default SysOp creds:")
  PrintE("LOGIN: sysop")
  PrintE(" PASS: YJQ57K")

  Open(6, "D2:USERS.DAT",8,0)

  PutD(6,'S)
  PutD(6,'Y)
  PutD(6,'S)
  PutD(6,'O)
  PutD(6,'P)
  PutD(6,32)
  PutD(6,32)
  PutD(6,32)
  PutD(6,32)
  PutD(6,32)
  PutD(6,'Y)
  PutD(6,'J)
  PutD(6,'Q)
  PutD(6,'5)
  PutD(6,'7)
  PutD(6,'K)
  PutD(6,32)
  PutD(6,32)
  PutD(6,32)
  PutD(6,32)


  PutD(6,155)
  PrintCDE(6,s)
  PrintBDE(6,o)
  Close(6)

; Note(5,@s,@o)
; Close(5)
; PrintF("Next: s=%U o=%B%E",s,o)

  PrintE("User data files created!")

RETURN


PROC InitCallLog()

  BYTE c=[0]
  c = 0

  Print("Init CALL.LOG [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  Close(6)
  Open(6,"D2:CALL.LOG",8,0)
  PrintDE(6,"")
  PrintDE(6," L A S T  C A L L E R S ")
  PrintDE(6,"")
  Close(6)

  PrintE("CALL.LOG initialized!")

RETURN



PROC InitSysInfo()

  BYTE c=[0]
  c = 0

  Print("Init SYSINFO.TXT [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  Close(6)
  Open(6,"D2:SYSINFO.TXT",8,0)
  PrintDE(6,"")
  PrintDE(6," S Y S T E M    I N F O ")
  PrintDE(6,"")
  Close(6)

  PrintE("SYSINFO.TXT initialized!")

RETURN



PROC InitWall()

  BYTE c=[0]
  c = 0

  Print("Init WALL.DAT [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  Close(6)
  Open(6,"D2:WALL.DAT",8,0)
  PrintDE(6,"")
  PrintDE(6," O N E L I N E R S  W A L L ")
  PrintDE(6,"")
  Close(6)

  PrintE("WALL.DAT initialized!")


RETURN


; creates a blank msg base with
; the file space filled and indexed
; at each fixed record start
; location.
;
PROC InitMsgBase()

  BYTE i=[0],o=[0]
  CARD c=[0],s=[0]

  Print("Init MSG BASE [0]? ")
  i = InputB()

  IF i = 0 THEN
    RETURN
  FI

  i = 0

  PutE()

  PrintE("Creating MSGBASE data and index")
  PrintE("files...")
  PrintE("This may take some time!")

  Close(5)
  Close(6)

  Open(5,"D2:MSGBASE.DAT",8,0)
  Open(6,"D2:MSGBASE.IDX",8,0)

  FOR i = 0 TO 49
  DO
    PutD(5,30) ;record separator

    Note(5,@s,@o)
    PrintF("MSG=%B s=%U o=%B%E",i,s,o)

    PrintCDE(6,s)
    PrintBDE(6,o)

    PrintDE(5,"         [END OF MESSAGES]")
    PutD(5,155)

    PutD(5,158) ;rs inverse for end

    FOR c = 0 TO 944
    DO
      PutD(5,32) ;space fill
    OD
  OD

  Close(5)
  Close(6)

  PrintE("Resetting HI msg to zero..")
  Open(5,"D2:MSGHI.DAT",8,0)
  PrintBDE(5,0)
  PrintBDE(5,0)
  Close(5)

  PrintE("MSGBASE files initialized!")

RETURN


PROC InitMsgTemp()
  BYTE i=[0],j=[0]

  Print("Init MSG TEMP [0]? ")
  i = InputB()

  IF i = 0 THEN
    RETURN
  FI

  i = 0

  PutE()
  Print("Initializing MSGTEMP.DAT...")

  Open(5,"D2:MSGTEMP.DAT",8,0)

  FOR i = 0 TO 40
  DO
    FOR j = 0 to 40
    DO
      PutD(5,'.)
    OD
    PutD(5,155)
  OD
  PutD(5,158)
  PutD(5,155)

  Close(5)
  PrintE("Done!")

RETURN



PROC InitAtascii()

  BYTE c=[0]
  c = 0

  Print("Copy ATASCII Files [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  PrintE("WARN: Files should be on D1!")
  PrintE("Copying files not needed!!")

  CopyFile("D1:LOGIN.ATA","D2:LOGIN.ATA")
  CopyFile("D1:TITLE.ATA","D2:TITLE.ATA")
  CopyFile("D1:MAINMENU.ATA","D2:MAINMENU.ATA")
  CopyFile("D1:MAINMENS.ATA","D2:MAINMENS.ATA")
  CopyFile("D1:LOGOUT.ATA","D2:LOGOUT.ATA")
  CopyFile("D1:MSGHELP.ATA","D2:MSGHELP.ATA")
  CopyFile("D1:SURVEY.ATA","D2:SURVEY.ATA")

  PrintE("ATASCII files copied!")



RETURN




PROC InitSurvey()

  BYTE c=[0]
  c = 0

  Print("Reset survey files [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  PrintE("Creating blank SURVEY.ATA...")
  Close(5)
  Open(5,"D2:SURVEY.ATA",8,0)
  Close(5)

  PrintE("Creating blank SURVEY.DAT..")
  Open(5,"D2:SURVEY.DAT",8,0)
  PrintBDE(5,0)
  Close(5)

  PrintE("Copying 'no results' history...")
  CopyFile("D1:SURVEY.HIS","D2:SURVEY.HIS")

  PrintE("Survey files initialized!")
  PrintE("Please run BBSADMIN to configure")

RETURN




PROC InitJokeOTD()

  BYTE c=[0]
  c = 0

  Print("Init Joke of the day [0]? ")
  c = InputB()

  IF c = 0 THEN
    RETURN
  FI

  PrintE("Copying default jokes...")
  CopyFile("D1:JOKES.DAT","D2:JOKES.DAT")

  PrintE("Joke data copied!")

RETURN




PROC main()

  Close(5)
  Close(6)

  Put(125)
  PutE()
  PrintE("  ACTION8 BBS  SETUP             ")
  PutE()

  InitUsers()
  InitCallLog()
  InitSysInfo()
  InitWall()
  InitMsgBase()
  InitMsgTemp()
  InitAtascii()
  InitSurvey()
  InitJokeOTD()

  PutE()
  PrintE("Setup complete.")
RETURN


