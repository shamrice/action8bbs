;
; Action 8 BBS Admin/User Editor
;
; Created: 2025-10-19
; Updated: 2026-02-21
;
;
;
;INCLUDE "D1:SYSBBS.ACT"

CARD USECT=[0]
BYTE UOFFSET=[0]

BYTE ARRAY MHFNAME(0)="D2:MSGHI.DAT"

BYTE ARRAY UFNAME(0)="D2:USERS.DAT"
BYTE ARRAY UANAME(0)="D2:USERS.ATR"
BYTE ARRAY ULNAME(0)="D2:USERS.LST"

BYTE ARRAY SFNAME(0)="D2:SURVEY.DAT"
BYTE ARRAY SANAME(0)="D2:SURVEY.ATA"
BYTE ARRAY SHNAME(0)="D2:SURVEY.HIS"

BYTE ARRAY UNAME(10)
BYTE ARRAY UATTR(32)


PROC SetAttr(BYTE idx)
  BYTE nVal=[0]

  nVal = 0

  IF idx = 0 THEN
    PrintE("ERROR>> Invalid id!")
    RETURN
  FI

  idx==-1

  PrintF("OLD VALUE: %B%E",UATTR(idx))
  Print("NEW VALUE: ")

  nVal = InputB()

  UATTR(idx) = nVal

RETURN


PROC ListUserAttrs()
  BYTE ARRAY a(4)
  BYTE i=[0]

  Put(125)
  PutE()
  PrintE("USER ATTRIBUTES:")
  PrintE("-------------------------------")

  PutE()
  Print(" 1)        CREATE MONTH: ")
  PrintBE(UATTR(0))

  Print(" 2)          CREATE DAY: ")
  PrintBE(UATTR(1))

  Print(" 3)         CREATE YEAR: ")
  PrintBE(UATTR(2))

  Print(" 4) LOGIN CNT HIGH BYTE: ")
  PrintBE(UATTR(3))

  Print(" 5)  LOGIN CNT LOW BYTE: ")
  PrintBE(UATTR(4))

  Print(" 6)            HIMSG ID: ")
  PrintBE(UATTR(5))

  Print(" 7)NUM MSGS POSTED HIGH: ")
  PrintBE(UATTR(6))

  Print(" 8) NUM MSGS POSTED LOW: ")
  PrintBE(UATTR(7))

  Print(" 9)   HIDE MSG HELP BAR: ")
  PrintBE(UATTR(8))

  Print("10)     USER PERM LEVEL: ")
  PrintBE(UATTR(9))

  Print("11)CURRENT SURVEY ANSRD: ")
  PrintBE(UATTR(10))

  PutE()
  PrintE("-------------------------------")
  PrintE(" 0) Exit to main menu")
  PrintE("-------------------------------")

RETURN



BYTE FUNC SetEditUser()
  BYTE i=[0],c=[0],v=[0],offTmp=[0]
  CARD sectTmp=[0]
  BYTE ARRAY user(11)

  v=0

  FOR i = 0 TO 31
  DO
    UATTR(i)=0
  OD

  FOR i = 0 TO 9
  DO
    UNAME(i)=32
  OD
  USECT=0
  UOFFSET=0

  Put(125)
  PutE()
  PrintE("EDIT USER:")
  Print("Username: ")

  InputS(user)

  FOR i = 1 TO user(0)
  DO
    c = user(i)
    IF c > 96 THEN
      c==-32
    FI
    UNAME(i-1)=c
  OD

;  PutE()
;  PrintF("ENTERED: %S%E",user)

;  FOR i = 0 TO 9
;  DO
;    Put(UNAME(i))
;  OD

  Close(5)
  Open(5,UFNAME,4,0)

  DO
    IF EOF(5) <> 0 THEN
      EXIT
    FI

    v = 1

    ;PutE()
    FOR i = 0 TO 9
    DO
      c = GetD(5)
;      Put(c)
      IF UNAME(i) <> c THEN
        ;Put('!)
        v = 0
      FI
    OD

    FOR i = 0 TO 9
    DO
      c=GetD(5)
      ;Put(c)
    OD
    GetD(5)

    sectTmp = InputCD(5)
    offTmp = InputBD(5)

    ;PutE()
    IF v = 1 THEN
      USECT = sectTmp
      UOFFSET = offTmp
;      PrintF("%U %B%E",sectTmp,offTmp)

      EXIT
    FI
  OD

  Close(5)

  PutE()
  IF v = 1 THEN
    ;PrintE(">> USER FOUND!")

    ; set user attrs
    Open(5,UANAME,12,0)
    Point(5,USECT,UOFFSET)

    FOR i = 0 TO 31
    DO
      UATTR(i) = GetD(5)
    OD

    Close(5)

  ELSE
    PrintE("ERROR>> USER NOT FOUND!")
    PutE()
    PrintE("<PRESS RETURN>")
    InputB()
  FI

RETURN(v)


PROC EditUser()
  BYTE v=[0],c=[0],i=[0],m=[0]
  v=0
  m=0

  v = SetEditUser()

  IF v = 0 THEN
    RETURN
  FI

  WHILE v <> 0
  DO

    ListUserAttrs()

    Print("EDIT>> ")
    c = InputB()

    IF c = 0 THEN
      v = 0
    ELSEIF c > 0 AND c <= 32 THEN
      m = 1
      SetAttr(c)
    ELSE
      PrintE("ERROR>> Invalid selection")
      PutE()
      PrintE("<PRESS RETURN>")
      InputB()
    FI
  OD

  IF m = 1 THEN
    PrintE("SAVING USER ATTRIBUTES...")

    Close(5)
    Open(5,UANAME,12,0)

    Point(5,USECT,UOFFSET)

    FOR i = 0 TO 31
    DO
      PutD(5,UATTR(i))
    OD
    Close(5)
  FI

RETURN



PROC ListUsers()
  BYTE c=[0]

  Put(125)
  PutE()
;  PrintE("-----------------------")

  Close(5)
  Open(5,ULNAME,4,0)

  DO
     IF EOF(5) <> 0 THEN
       EXIT
     FI

     c = GetD(5)
     Put(c)
  OD
  Close(5)

  PrintE("-----------------------")
  Print("<PRESS RETURN>")
  InputB()


RETURN


PROC DispHiMsgId()
  BYTE c=[0]

  Put(125)
  PutE()
  PrintE("-----------------------")

  Close(5)
  Open(5,MHFNAME,4,0)

  DO
     IF EOF(5) <> 0 THEN
       EXIT
     FI

     c = GetD(5)
     Put(c)
  OD
  Close(5)

  PrintE("-----------------------")
  Print("<PRESS RETURN>")
  InputB()

RETURN



PROC SetSurvey()

  BYTE c=[0],i=[0]
  c=0
  i=0

  Put(125)
  PutE()
  PrintE("  SET NEW SURVEY              ")
  PutE()
  Print("Reset all user ans attr?[0] ")

  c = InputB()
  PutE()
  IF c <> 0 THEN
    PrintE("Resetting user attributes..")
    Close(5)
    Open(5,UFNAME,4,0)

    DO
      IF EOF(5) <> 0 THEN
        EXIT
      FI

      Print("USER: ")
      FOR i = 0 TO 19
      DO
        c=GetD(5)
        Put(c)
      OD
      PutE()
      GetD(5)

      USECT = InputCD(5)
      UOFFSET = InputBD(5)

      IF USECT = 0 AND UOFFSET = 0 THEN
        PrintE("==END OF USER LIST==")
        EXIT
      FI

      Close(6)
      Open(6,UANAME,12,0)

      PrintF("POINT: S=%U O=%B%E",USECT,UOFFSET)
      Point(6,USECT,UOFFSET)

      PrintE("USER ATTRS:")
      FOR i = 0 TO 31
      DO
        IF i = 10 THEN
          PrintF("%EZERO: %B%E",c)
          PutD(6,0)
        ELSE
          c = GetD(6)
          Put(c)
        FI
      OD
      PutE()
      Close(6)

    OD

    Close(5)
    PutE()
    PrintE(">>All user survey attrs reset!")
  FI

  PutE()
  Print("Append current to result history?[0]")
  c = InputB()

  IF c = 1 THEN
    Close(5)
    Close(6)
    Open(5,SANAME,4,0)
    Open(6,SHNAME,9,0)

    PrintDE(6,"")

    ; Copy SURVEY.ATA in
    DO
      IF EOF(5) <> 0 THEN
        EXIT
      FI

      c = GetD(5)
      PutD(6,c)
    OD

    ; Copy results in.
    Close(5)
    Open(5,SFNAME,4,0)

    PrintDE(6," VOTES:  ")
    c = InputBD(5) ;num options
    IF c > 0 THEN
      FOR i = 1 TO c
      DO
        c = InputBD(5)
        PrintD(6,"  OPTION #")
        PrintBD(6,i)
        PrintD(6,": ")
        PrintBDE(6,c)
      OD
    FI
    PutD(6,155)

    Close(5)
    Close(6)

    PrintE(">>Current results saved.")

  FI



  PutE()
  Print("Reset any survey files?[0]")
  c = InputB()

  IF c = 1 THEN

    Print("Create new SURVEY.ATA?[0] ")
    c = InputB()

    Close(5)

    IF c = 1 THEN
      Open(5,SANAME,8,0)
      Close(5)
      PrintE("++NEW SURVEY.ATA CREATED")
    FI


    Print("Reset survey data?[0] ")
    c = InputB()

    IF c = 1 THEN

      Print("  How many options?[0] ")
      c = InputB()

      Open(5,SFNAME,8,0)
      PrintBDE(5,c)
      IF c > 0 THEN
        c==-1
        FOR i = 0 TO c
        DO
          PrintBDE(5,0)
        OD
      FI

      Close(5)
      PrintE("++SURVEY DATA RESET")
    FI


    Print("Reset survey history?[0] ")
    c = InputB()

    IF c = 1 THEN
      Open(5,SHNAME,8,0)
      Close(5)
      PrintE("++SURVEY HISTORY RESET")
    FI

  FI

  PutE()


RETURN




PROC Main()
  BYTE c=[0],v=[0],running=[1]

  running=1


  WHILE running = 1
  DO
    Put(125)
    PrintE("     ACTION8 BBS ADMIN            ")
    PutE()
    PrintE("1) List Users")
    PrintE("2) Edit User")
    PrintE("3) Display Hi Msg Id Data")
    PrintE("4) Setup New Survey")
    PutE()
    PrintE("0) Quit")
    PutE()
    Print("MAIN>> ")
    c = InputB()

    IF c = 1 THEN
      ListUsers()
    ELSEIF c = 2 THEN
      EditUser()
    ELSEIF c = 3 THEN
      DispHiMsgId()
    ELSEIF c = 4 THEN
      SetSurvey()
    ELSEIF c = 0 THEN
      running = 0
    FI
  OD

  PutE()
  PrintE("Exit...")
RETURN

